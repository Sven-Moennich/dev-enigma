#
#                              Copyright (C) 2016 by Rafael Santiago
#
# This is free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#

include ~/toolsets/linux/linux-module.hsl
include run_test.hsl
include installer.hsl
include mkclean.hsl

var includes type list;
var cflags type list;
var libraries type list;
var ldflags type list;

project dev-enigma : toolset "linux-module" : "mod.c", $includes, $cflags, $libraries, $ldflags, "enigma";

dev-enigma.prologue() {
    cleaner();

    device_installer();

    # INFO(Santiago): The KBuild runs relative to the kernel source tree, for this reason is important to
    #                 define the local include directories as full-paths.

    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "eel"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "ebuf"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "dev_ctx"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "fops_impl"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "mod_traps"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "enigmactl"));
}

dev-enigma.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        run_test();
        forge_user_enigmactl();
        hefesto.sys.echo("\n" +
                         "*** All /dev/enigma was built. Let's celebrate, we are still alive!\n" +
                         "    For installing issues you should use: 'hefesto --install'\n");
    }
}

local function forge_user_enigmactl() : result type none {
    var cwd type string;
    $cwd = hefesto.sys.pwd();

    if (hefesto.sys.cd("enigmactl/user")) {
        hefesto.sys.forge("enigmactl", "Forgefile.hsl", "--obj-output-dir=o --bin-output-dir=bin");
        hefesto.sys.cd($cwd);
        if (hefesto.sys.last_forge_result() != 0) {
            hefesto.project.abort(hefesto.sys.last_forge_result());
        }
    }
}

local function cleaner() : result type none {
    var option type list;

    $option = hefesto.sys.get_option("clean");

    if ($option.count() > 0) {
        mkclean();
        hefesto.sys.echo("*** Clean.\n");
        hefesto.project.abort(0);
    }
}
