#
#                              Copyright (C) 2016 by Rafael Santiago
#
# This is free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#

include on linux ~/toolsets/linux/linux-module.hsl
include on freebsd ~/toolsets/freebsd/freebsd-module.hsl
include run_test.hsl
include installer.hsl
include mkclean.hsl

var includes type list;
var cflags type list;
var libraries type list;
var ldflags type list;

var LKM_toolset type string;

dev-enigma.preloading() {
    if (hefesto.sys.os_name() == "linux") {
        $LKM_toolset = "linux-lkm";
    } else if (hefesto.sys.os_name() == "freebsd") {
        $LKM_toolset = "freebsd-kld";
    } else {
        $LKM_toolset = "cry-baby-cry";
    }
}

project dev-enigma : toolset $LKM_toolset : "mod.c", $includes, $cflags, $libraries, $ldflags, "enigma";

dev-enigma.prologue() {
    cleaner();

    device_installer();

    # INFO(Santiago): The KBuild runs relative to the kernel source tree, for this reason is important to
    #                 define the local include directories as full-paths.

    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "eel"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "ebuf"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "dev_ctx"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), hefesto.sys.make_path("fops_impl", hefesto.sys.os_name())));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), hefesto.sys.make_path("mod_traps", hefesto.sys.os_name())));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "enigmactl"));
}

dev-enigma.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        run_test();
        forge_user_enigmactl();
        hefesto.sys.echo("\n" +
                         "*** All /dev/enigma was built. Let's celebrate, we are still alive!\n" +
                         "    For installing issues you should use: 'hefesto --install'\n");
    }
}

local function forge_user_enigmactl() : result type none {
    var cwd type string;
    $cwd = hefesto.sys.pwd();

    if (hefesto.sys.cd("enigmactl/user")) {
        hefesto.sys.forge("enigmactl", "Forgefile.hsl", "--obj-output-dir=o --bin-output-dir=bin");
        hefesto.sys.cd($cwd);
        if (hefesto.sys.last_forge_result() != 0) {
            hefesto.project.abort(hefesto.sys.last_forge_result());
        }
    }
}
