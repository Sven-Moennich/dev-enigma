#
#                              Copyright (C) 2016 by Rafael Santiago
#
# This is free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#

include ~/toolsets/linux/linux-module.hsl
include run_test.hsl
include installer.hsl

var includes type list;
var cflags type list;
var libraries type list;
var ldflags type list;

project dev-enigma : toolset "linux-module" : "mod.c", $includes, $cflags, $libraries, $ldflags, "enigma";

dev-enigma.prologue() {
    device_installer();

    # INFO(Santiago): The KBuild runs relative to the kernel source tree, for this reason is important to
    #                 define the local include directories as full-paths.

    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "eel"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "ebuf"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "dev_ctx"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "fops_impl"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "mod_traps"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "enigmactl"));
}

dev-enigma.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        forge_user_enigmactl();
        run_test();
    }
}

local function forge_user_enigmactl() : result type none {
    var cwd type string;
    $cwd = hefesto.sys.pwd();

    if (hefesto.sys.cd("enigmactl/user")) {
        hefesto.sys.forge("enigmactl", "Forgefile.hsl", "--obj-output-dir=o --bin-output-dir=bin");
        hefesto.sys.cd($cwd);
        if (hefesto.sys.last_forge_result() != 0) {
            hefesto.project.abort(hefesto.sys.last_forge_result());
        }
    }
}
